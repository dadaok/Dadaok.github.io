---
layout:   post
title:    "Querydsl"
subtitle: "Querydsl"
category: Jpa
more_posts: posts.md
tags:     Jpa
---
# [JPA 활용] 8. Querydsl 설정

<!--more-->
<!-- Table of contents -->
* this unordered seed list will be replaced by the toc
{:toc}

<!-- text -->

## Querydsl
> Querydsl은 자바 언어를 위한 타입-세이프(query generation) 쿼리 생성 라이브러리로, SQL, JPA, JDO와 같은 다양한 저장 기술에 대한 쿼리를 자바 코드로 작성할 수 있게 해준다. 이는 컴파일 시점에 오류를 감지할 수 있게 함으로써 사용된다. 실전에서 널리 사용되는 이유는 동적 쿼리의 쉬운 생성을 지원하고 코드 기반의 쿼리 관리를 가능하게 함으로써 유지보수성과 개발 효율성을 향상시키기 때문이다.  

## 설정
### Gradle 전체 설정 - 스프링 부트 3.x
``` java
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}
group = 'study'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //test 롬복 사용
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    //Querydsl 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}
tasks.named('test') {
    useJUnitPlatform()
}
clean {
    delete file('src/main/generated')
}
```

### Gradle 전체 설정 - 스프링 부트 2.x

``` java
plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    //querydsl 추가
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'java'
}

group = 'study'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    //querydsl 추가
    implementation 'com.querydsl:querydsl-jpa'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}
test {
 useJUnitPlatform()
}
//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
configurations {
    querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
//querydsl 추가 끝
```

### 주의! 스프링 부트 3.2부터 JDK 17과 Gradle 옵션을 선택하자.
> 스프링 부트 3.2 부터 JDK 17 이상과, 빌드시 IntelliJ IDEA가 아니라 Gradle을 선택해야 한다.

### Q 타입 생성 방법
> Q타입 생성은 타입 안전성을 보장하고, 쿼리 작성 시 컴파일 타임에 오류를 발견할 수 있도록 한다. 이는 개발자가 코드를 통해 직접 쿼리를 작성할 때 오타나 잘못된 필드명을 사용하는 등의 실수를 사전에 방지할 수 있게 해, 더 안정적이고 효율적인 쿼리 작성을 가능하게 한다.  

- Gradle IntelliJ 사용법
  - Gradle Tasks build clean
  - Gradle Tasks other compileQueryds